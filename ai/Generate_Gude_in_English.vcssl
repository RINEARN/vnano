coding UTF-8;

// ================================================================================
// Textfiles to Knowledge-JSONs Converter Ver.1.0.3
// Made by: RINEARN (Fumihiro Matsui)
// Licence: Unlicense or CC0 (Dual License)
// ================================================================================

import System;
import File;
import Text;
import data.Queue;


// --------------------------------------------------------------------------------
// Defines the constant parameters.
// --------------------------------------------------------------------------------

const string OUTPUT_FILE_PATH = "./Guide_in_English.json";
const string OUTPUT_ENCODING = "UTF-8";

const string REFTABLE_OUTPUT_FILE_PATH = "./REFTABLE_Guide_in_English.txt";
const string REFTABLE_OUTPUT_ENCODING = "UTF-8";

const string INPUT_BASE_DIRECTORY_PATH = "./";
const string INPUT_ENCODING = "UTF-8";

// --------------------------------------------------------------------------------
// Defines the pages to be processed.
// --------------------------------------------------------------------------------

Queue<Page> pageQueue;       // (Path to a Markdown file, URL in ref-table, Title, Short description)
enqueue<Page>(pageQueue, newPage("./webpages/frontpage_ENGLISH.md", "https://www.vcssl.org/en-us/vnano/", "Vnano Official Website", "The frontpage of the official website, introduing features of Vnano."));
enqueue<Page>(pageQueue, newPage("../README.md", "https://github.com/RINEARN/vnano/blob/main/README.md", "README of GitHub repository", "The frontpage of the source code repository on GitHub. Explaining how to build Vnano Engine, and how to use it on apps."));
enqueue<Page>(pageQueue, newPage("../doc/FEATURE.md", "https://www.vcssl.org/ja-jp/vnano/doc/tutorial/feature", "Main Features of Vnano Engine", "Introducing main features of Vnano Engine, with example code of apps."));
enqueue<Page>(pageQueue, newPage("../doc/LANGUAGE.md", "https://www.vcssl.org/ja-jp/vnano/doc/tutorial/language", "Vnano as a Language", "Explaining syntax and language features of Vnano."));
enqueue<Page>(pageQueue, newPage("../doc/SPEC.md", "https://www.vcssl.org/ja-jp/vnano/spec/", "Main Features of Vnano Engine", "This page is the specification document of VnanoEngine class, Options, and so on."));
enqueue<Page>(pageQueue, newPage("../ExampleScript1.vnano", "https://github.com/RINEARN/vnano/blob/main/ExampleScript1.vnano", "Example Script Code 1", "A very simple example of a Vnano script."));
enqueue<Page>(pageQueue, newPage("../ExampleApp1.java", "https://github.com/RINEARN/vnano/blob/main/ExampleApp1.java", "Example App-Side Code 1", "The most simple example code of an application using Vnano Engine."));
enqueue<Page>(pageQueue, newPage("../ExampleApp2.java", "https://github.com/RINEARN/vnano/blob/main/ExampleApp2.java", "Example App-Side Code 2", "An example application using Vnano Engine, to calculate an expression inputted by the user."));
enqueue<Page>(pageQueue, newPage("../ExampleApp3.java", "https://github.com/RINEARN/vnano/blob/main/ExampleApp3.java", "Example App-Side Code 3", "A example accessing to fields/methods of application-side, from an expression executed by Vnano Engine."));
enqueue<Page>(pageQueue, newPage("../ExampleApp4.java", "https://github.com/RINEARN/vnano/blob/main/ExampleApp4.java", "Example App-Side Code 4", "An example accessing to fields/methods of dynamically loaded classes (plug-ins), from an expression executed by Vnano Engine."));
enqueue<Page>(pageQueue, newPage("../ExampleApp5.java", "https://github.com/RINEARN/vnano/blob/main/ExampleApp5.java", "Example App-Side Code 5", "An example executing a script using Vnano Engine."));
enqueue<Page>(pageQueue, newPage("../ExampleApp6.java", "https://github.com/RINEARN/vnano/blob/main/ExampleApp6.java", "Example App-Side Code 6", "An example executing a script, in which a function/variable provided by a library script \"lib/ExampleLibrary1.vnano\" are used."));
enqueue<Page>(pageQueue, newPage("../ExampleApp7.java", "https://github.com/RINEARN/vnano/blob/main/ExampleApp7.java", "Example App-Side Code 7", "An example executing the same expression repetitively in high speed."));


struct Page {
	string relativeFilePath;
	string url;
	string title;
	string description;
	string text;
	int hierarchyOffset;
}

Page newPage(string relativeFilePath, string url, string title, string description) {
	Page page;
	page.relativeFilePath = relativeFilePath;
	page.title = title;
	page.description = description;
	page.url = url;
	page.hierarchyOffset = 1; // Always 1 for the current implementation.
	return page;
}




// --------------------------------------------------------------------------------
// Processes the aboves.
// --------------------------------------------------------------------------------

void main() {
	int pageCount = size<Page>(pageQueue);
	println("PAGE COUNT = " + pageCount);
	
	int outputFile = open(OUTPUT_FILE_PATH, System.WRITE, OUTPUT_ENCODING);
	int refTableFile = open(REFTABLE_OUTPUT_FILE_PATH, System.WRITE, REFTABLE_OUTPUT_ENCODING);
	
	writeln(outputFile, "{");
	
	for (int ipage; ipage<pageCount; ipage++) {
		
		// Extract one page from the queue (global variable).
		Page page = dequeue<Page>(pageQueue);
		
		// Open the page file.
		string absoluteFilePath = getFilePath(page.relativeFilePath, INPUT_BASE_DIRECTORY_PATH, File.ABSOLUTE);
		int inputFile = open(absoluteFilePath, System.READ, INPUT_ENCODING);
		
		// Load, convert, and output the content of the page.
		println("PROCESSING PAGE = " + absoluteFilePath);
		processPage(
			inputFile, outputFile,
			ipage, (ipage == pageCount - 1),
			page.title, page.description, page.url, page.hierarchyOffset
		);
		
		// Write a line in the reference-table.
		writeln(refTableFile, "* [" + page.title + "](" + page.url + "): " + page.description);
		
		// Close the page file.
		close(inputFile);
	}
	
	// Close the knowledge file.
	writeln(outputFile, "}");
	close(outputFile);
	close(refTableFile);
	
	println("COMPLETED !");
}


void processPage(int inputFile, int outputFile, int pageIndex, boolean isLast, string title, string description, string url, int hierarchyOffset) {
	int lineCount = countln(inputFile);
	int headerLineCount = 0;
	
	string offsetTab = "";
	for (int ihierarchy=0; ihierarchy<hierarchyOffset; ihierarchy++) {
		offsetTab += "	";
	}
	string offsetTabTab = offsetTab + "	";

	writeln(outputFile, offsetTab + "\"page" + pageIndex + "\": {");

	writeln(outputFile, offsetTabTab + "\"title\": \"" + title + "\",");
	writeln(outputFile, offsetTabTab + "\"description\": \"" + description + "\",");
	//writeln(outputFile, offsetTabTab + "\"url\": \"" + url + "\",");
	
	write(outputFile, offsetTabTab + "\"text\": \"");
	
	// Process all the lines in the input file.
	for (int iline=0; iline<lineCount; iline++) {
		string line = readln(inputFile);
		line = escape(line);
		write(outputFile, line + "\\n");
	}
	
	writeln(outputFile, "\"");
	if (isLast) {
		writeln(outputFile, offsetTab + "}");
	} else {
		writeln(outputFile, offsetTab + "},");
	}
}

private string escape(string line) {
	line = replaceText(line, "\\", "\\\\", Text.ALL);
	line = replaceText(line, "\"", "\\\"", Text.ALL);
	line = replaceText(line, "/", "\\/", Text.ALL);
	line = replaceText(line, "	", "\\t", Text.ALL);
	line = replaceText(line, CR+LF, LF, Text.ALL);
	line = replaceText(line, LF, "\\n", Text.ALL);
	return line;
}





